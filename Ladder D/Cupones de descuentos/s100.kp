iniciar-programa

	define-nueva-instruccion der como
	inicio
		repetir 3 veces
			gira-izquierda;
	fin;

	define-nueva-instruccion med como
	inicio
		repetir 2 veces
			gira-izquierda;
	fin;

	define-nueva-instruccion compara(n) como
	inicio
		si junto-a-zumbador y no si-es-cero(n) entonces
		inicio
			coge-zumbador;
			compara(precede(n));
			deja-zumbador;
		fin
		sino
			si no si-es-cero(n) entonces
		inicio
			gira-izquierda;
		fin;
	fin;

	define-nueva-instruccion cmpContar(n) como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			cmpContar(sucede(n));
		fin
		sino
		inicio
			repetir n veces
				deja-zumbador;
			avanza;
			compara(n);
		fin;
	fin;

	define-nueva-instruccion avanzaZumbadores como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			avanzaZumbadores;
			deja-zumbador;
		fin
		sino
		inicio
			avanza;
		fin;
	fin;

	define-nueva-instruccion cambiar(n) como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			cambiar(sucede(n));
		fin
		sino
		inicio
			avanza;
			repetir 2 veces
				gira-izquierda;
			avanzaZumbadores;
			med;
			avanza;
			repetir n veces
				deja-zumbador;
			med;
			avanza;
		fin;
	fin;

	define-nueva-instruccion burbuja(n) como
	inicio
		si si-es-cero(n) entonces
			sal-de-instruccion;
		repetir n veces
		inicio
			cmpContar(0);
			si orientado-al-norte entonces
			inicio
				gira-izquierda;
				cambiar(0);
			fin;
		fin;
		med;
		mientras frente-libre hacer
			avanza;
		med;
		burbuja(precede(n));
	fin;

	define-nueva-instruccion burbujaContar(n) como
	inicio
		si frente-libre entonces
		inicio
			avanza;
			si junto-a-zumbador entonces
				burbujaContar(sucede(n))
			sino
			inicio
				med;
				mientras frente-libre hacer
					avanza;
				med;
				burbuja(n);
			fin;
		fin
		sino
		inicio
			med;
			mientras frente-libre hacer
				avanza;
			med;
			burbuja(n);
		fin;
	fin;

	define-nueva-instruccion quita(n) como
	inicio
		repetir precede(n) veces
		inicio
			si orientado-al-oeste y frente-libre entonces
			inicio
				avanza;
			fin
			sino
				si orientado-al-oeste entonces
			inicio
				gira-izquierda;
				si frente-libre entonces
					avanza
				sino
					gira-izquierda;
			fin;
		fin;
		si orientado-al-oeste entonces
		inicio
			mientras junto-a-zumbador hacer
				coge-zumbador;
			si frente-libre entonces
				avanza
			sino
				gira-izquierda;
		fin;
	fin;

	define-nueva-instruccion alFinal como
	inicio
		gira-izquierda;
		avanza;
		gira-izquierda;
		mientras frente-libre y junto-a-zumbador hacer
			avanza;
		med;
		si no junto-a-zumbador entonces
			avanza;
	fin;

	define-nueva-instruccion cupones(n) como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			cupones(sucede(n));
		fin
		sino
			si frente-libre entonces
		inicio
			avanza;
			cupones(0);
			quita(n);
		fin
		sino
		inicio
			alFinal;
			quita(n);
		fin;
	fin;

	inicia-ejecucion
		der;
		burbujaContar(0);
		gira-izquierda;
		avanza;
		der;
		burbujaContar(0);
		mientras frente-libre y junto-a-zumbador hacer
		inicio
			avanza;
		fin;
		med;
		si no junto-a-zumbador entonces
			avanza;
		cupones(0);
		mientras no orientado-al-este hacer
			gira-izquierda;
		mientras frente-libre hacer
			avanza;
		med;
		mientras frente-libre hacer
			avanzaZumbadores;
		apagate;
	termina-ejecucion
finalizar-programa