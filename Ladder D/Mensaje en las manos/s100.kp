iniciar-programa

	
	{ @saltatela }
	define-nueva-instruccion gira(n) como
	inicio
		repetir n veces
			gira-izquierda;
	fin;
	{ @saltatela }
	define-nueva-instruccion norte como
	inicio
		mientras no-orientado-al-norte hacer 
			gira-izquierda;
	fin;
	define-nueva-instruccion fila(n) como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			fila(sucede(n));
			
		fin 
		sino
		inicio
			si frente-libre entonces
			inicio
				avanza;
				si frente-libre entonces
					avanza;
				si junto-a-zumbador entonces
					fila(0);
			fin;
			gira(2);
			mientras frente-libre hacer
				avanza;
			gira(2);
			repetir precede(n) veces
				avanza;
			
			si no si-es-cero(precede(n)) entonces
			deja-zumbador;
		fin;
	fin;

	define-nueva-instruccion max(n) como
	inicio
		si junto-a-zumbador y no si-es-cero(n) entonces
		inicio
			coge-zumbador;
			max(precede(n));
			deja-zumbador;
		fin 
		sino
			repetir n veces
				deja-zumbador;
	fin;
	
	define-nueva-instruccion empuja(n) como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			empuja(sucede(n));
		fin
		sino
		inicio
			avanza;
			max(n);
		fin;
	fin;

	define-nueva-instruccion suma(n) como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			suma(sucede(n));
		fin
		sino
		inicio
			avanza;
			repetir n veces
				deja-zumbador;
		fin;
	fin;

	define-nueva-instruccion mueve(n) como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			mueve(sucede(n));
		fin 
		sino
		inicio
			avanza;			
			mientras junto-a-zumbador hacer 
				coge-zumbador;
			repetir n veces deja-zumbador;
			gira(2);
			avanza;
		fin;
	fin;

	define-nueva-instruccion comparar(n) como
	inicio
		si junto-a-zumbador y no si-es-cero(n) entonces
		inicio
			coge-zumbador;
			comparar(precede(n));
			deja-zumbador;
		fin
		sino si no si-es-cero(n) entonces
			gira(2);
	fin;

	define-nueva-instruccion por10 como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			por10;
			repetir 10 veces
				deja-zumbador;
		fin;
		
	fin;
	define-nueva-instruccion procesa(n) como
	inicio
		si junto-a-zumbador entonces
		inicio
			coge-zumbador;
			procesa(sucede(n));
		fin
		sino
		inicio
			repetir n veces
				deja-zumbador;
			avanza;
			comparar(n);
			si orientado-al-oeste entonces
			inicio
				mueve(0);
				repetir n veces deja-zumbador;
			fin;
			gira(2);
			avanza;
			por10;
			gira(2);
			avanza;
			gira(2);
			suma(0);
			gira(2);
			avanza;
			si frente-libre entonces
				avanza;
		fin;
	fin;
		
	
	inicia-ejecucion
		mientras orientado-al-norte hacer
		inicio		
			gira(3);
			mientras junto-a-zumbador hacer
				procesa(0);
			gira(2);
			mientras frente-libre hacer
				avanza;
			gira(2);
			fila(0);
			gira(2);
			mientras frente-libre hacer
				avanza;
			gira(3);
			si frente-libre entonces
				avanza
			sino
				gira-izquierda;
			si no-junto-a-zumbador entonces
				gira-izquierda;
		fin;
			norte;
		repetir 99 veces
		inicio
			mientras frente-libre hacer
				avanza;
			gira(2);
			mientras frente-libre hacer
				empuja(0);
			gira-izquierda;
			avanza;
			gira-izquierda;
		fin;
		gira-izquierda;
		mientras frente-libre hacer
			suma(0);
		apagate;
	termina-ejecucion
finalizar-programa